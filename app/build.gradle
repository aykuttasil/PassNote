apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: "androidx.navigation.safeargs"


android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }
    /*
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    */

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled false
            // signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        finalProduction {
            dimension "default"
            applicationId "aykuttasil.com.passnote"
            resValue "string", "app_name", "PassNote"
        }

        demoProduction {
            dimension "default"
            applicationId "aykuttasil.com.passnote.demoproduction"
            resValue "string", "app_name", "PassNote Demo"
        }

        demoTesting {
            dimension "default"
            applicationId "aykuttasil.com.passnote.demotesting"
            resValue "string", "app_name", "PassNote Test"
        }

        mock {
            dimension "default"
            applicationId "aykuttasil.com.passnote.mock"
            resValue "string", "app_name", "PassNote Mock"
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/MANIFEST.MF'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = output.outputFile.name
                    .replace(".apk", "-${variant.versionName}-${variant.versionCode}.apk")
        }
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    releaseImplementation 'com.github.ajitsing:sherlock-no-op:1.0.4@aar'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.1.0'
    debugImplementation 'com.readystatesoftware.chuck:library:1.1.0'
    debugImplementation('com.github.ajitsing:sherlock:1.0.4@aar') {
        transitive = true
    }

    // Stetho
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    // DataBinding
    kapt 'androidx.databinding:databinding-compiler:3.2.1'

    // AndroidX
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core:1.0.1'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.multidex:multidex:2.0.0'

    // Http
    implementation 'com.squareup.moshi:moshi-adapters:1.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'

    // Play Services - Firebase
    implementation 'com.google.android.gms:play-services-base:16.0.1'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-places:16.0.0'

    // Firebase
    implementation 'com.google.firebase:firebase-database:16.0.5'

    // Dagger
    def daggerVersion = "2.16"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"

    // Android Arch

    // LiveData - ViewModel
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-reactivestreams:2.0.0'
    implementation 'androidx.room:room-runtime:2.1.0-alpha02'
    kapt "androidx.room:room-compiler:2.1.0-alpha02"
    implementation 'androidx.room:room-rxjava2:2.1.0-alpha02'

    // implementation "android.arch.paging:runtime:1.0.0-alpha4-1"

    // Navigation
    def navigation_version = "1.0.0-alpha07"
    implementation "android.arch.navigation:navigation-fragment:$navigation_version"
    implementation "android.arch.navigation:navigation-ui:$navigation_version"

    def work_version = "1.0.0-alpha11"
    implementation "android.arch.work:work-runtime-ktx:$work_version"
    implementation "android.arch.work:work-firebase:$work_version"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Anko

    // (including Commons, Layouts, SQLite)
    implementation "org.jetbrains.anko:anko:$anko_version"

    // Kotlin Extensions
    implementation 'androidx.core:core-ktx:1.0.1'

    // implementation 'androidx.core:core-ktx:1.0.0-alpha1'

    // Rx
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.4@aar'

    // Butterknife
    implementation 'com.jakewharton:butterknife:8.8.1'
    kapt "com.jakewharton:butterknife-compiler:8.8.1"

    // Glide

    //implementation "com.github.bumptech.glide:glide:4.7.1"

    //kapt "com.github.bumptech.glide:compiler:4.7.1"

    //implementation "com.github.bumptech.glide:okhttp3-integration:4.7.1"

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }
}

// apply plugin: 'com.google.gms.google-services'

